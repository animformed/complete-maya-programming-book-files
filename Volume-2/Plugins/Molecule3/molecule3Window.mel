//
// Copyright (C) 2004 David Gould 
// 

global proc molecule3Window()
{
	string $layout = getOptionBox();
	setParent $layout;
	setUITemplate -pushTemplate DefaultTemplate;
	waitCursor -state 1;

	// Retrieve the option variables
	float $rad = `optionVar -q "molecule3Radius"`;
	int $segs = `optionVar -q "molecule3Segments"`;
	float $br = `optionVar -q "molecule3BallRatio"`;
	
	// Create the controls for the options
	string $parent = `columnLayout -adjustableColumn 1`;
	floatSliderGrp -label "Radius" -value $rad -min 0.00001 -max 100 -fieldMinValue 0.00001 -fieldMaxValue 10000000 molecule3Radius;
	intSliderGrp -label "Segments" -value $segs -min 1 -max 60 -fieldMinValue 1 -fieldMaxValue 10000 molecule3Segments;
	floatSliderGrp -label "Ball-Rod Ratio" -value $br -min 0.00001 -max 10 -fieldMinValue 0.00001 -fieldMaxValue 10000000 molecule3BallRatio;

	waitCursor -state 0;
	setUITemplate -popTemplate;

	string $applyBtn = getOptionBoxApplyBtn();
	button -edit -label "Create"
		   -command ("molecule3SaveOptions( \"" + $parent + "\" ); molecule3Execute();")
		   $applyBtn;
	string $saveBtn = getOptionBoxSaveBtn();
	button -edit 
		   -command ("molecule3SaveOptions( \"" + $parent + "\");")
		   $saveBtn;
	string $resetBtn = getOptionBoxResetBtn();
	button -edit 
		   -command "molecule3SetupOptions(1); molecule3UpdateWindow();"
		   $resetBtn;
			 
	setOptionBoxTitle( "Molecule3 Options" );

	showOptionBox();
}

global proc molecule3SaveOptions( string $parent )
{		
	// Get values of controls
	setParent $parent;
	float $rad = `floatSliderGrp -query -value molecule3Radius`;
	int $segs = `intSliderGrp -query -value molecule3Segments`;
	float $br = `floatSliderGrp -query -value molecule3BallRatio`;
	
	// Update option variables
	optionVar -floatValue molecule3Radius $rad;
	optionVar -intValue molecule3Segments $segs;
	optionVar -floatValue molecule3BallRatio $br;
}

global proc molecule3UpdateWindow()
{
	// Retrieve the option variables
	float $rad = `optionVar -q "molecule3Radius"`;
	int $segs = `optionVar -q "molecule3Segments"`;
	float $br = `optionVar -q "molecule3BallRatio"`;

	floatSliderGrp -edit -value $rad "molecule3Radius";
	intSliderGrp -edit -value $segs "molecule3Segments";
	floatSliderGrp -edit -value $br "molecule3BallRatio";
}